{
    parserClass = "com.github.cucapra.futil.parser.FutilParser"
    parserUtilClass = "com.github.cucapra.futil.psi.FutilParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "Futil"
    psiImplClassSuffix = "Node"
    psiPackage = "com.github.cucapra.futil.psi"
    psiImplPackage = "com.github.cucapra.futil.psi.impl"

    elementTypeHolderClass = "com.github.cucapra.futil.psi.FutilTypes"
    elementTypeClass = "com.github.cucapra.futil.psi.FutilElementType"
    tokenTypeClass = "com.github.cucapra.futil.psi.FutilElementType"

    psiImplUtilClass = "com.github.cucapra.futil.psi.FutilASTExtension"
    generateTokenAccessors = false
    tokens = [
        COMMENT = 'regexp:(//|#)[^\r\n]*'
        BLOCK_COMMENT = 'regexp:[/][*][^*]*[*]+([^/*][^*]*[*]+)*[/]'

        AS = "as"
        NULL = 'null'
        BOOLEAN = 'regexp:true|false'

        SYMBOL = 'Symbol'
        STRING = 'String'
        BITS = 'Bits'
        NUMBER = 'Number'

        PARENTHESIS_L = '('
        PARENTHESIS_R = ')'
        BRACKET_L = '['
        BRACKET_R = ']'
        BRACE_L = '{'
        BRACE_R = '}'
        ACCENT = '^'
        ANGLE_L = '<'
        ANGLE_R = '>'
        QUOTATION = '"'
        ESCAPE = '\'
        NON_ESCAPE = 'regexp:[^\\]'
        EQ = '='
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        CITE = '$'
        DOT = '.'
        STAR = '*'
        QUESTION = '?'
        AT = '@'
//      SPACE='regexp:\s+'
//      ANY = 'regexp:.'
    ]
}

Futil ::= top_stat*;
private top_stat ::=
    import_statement SEMICOLON
  | component_statement
  ;
private inner_stat ::=
    seq_stat
  | annotation? expression SEMICOLON
  ;
private seq_stat ::=
    group_statement
  | comb_statement
  | seq_statement
  | while_statement
  | normal_statement
  | identifier SEMICOLON
  ;
// ---------------------------------------------------------------------------------------------------------------------
import_statement ::= IMPORT STRING;
IMPORT ::= "import"
// ---------------------------------------------------------------------------------------------------------------------
component_statement ::= COMPONENT component_name component_target <<brace inner_stat>>;
component_name ::= identifier <<parenthesis identifier SEMICOLON>>
component_target ::= TO <<parenthesis identifier SEMICOLON>>
COMPONENT ::= "component"
// ---------------------------------------------------------------------------------------------------------------------
group_statement ::= GROUP identifier <<brace inner_stat>>;
GROUP  ::= "group";
// ---------------------------------------------------------------------------------------------------------------------
comb_statement ::= COMB GROUP identifier <<brace inner_stat>>;
COMB ::= "comb";
// ---------------------------------------------------------------------------------------------------------------------
seq_statement ::= SEQ <<brace seq_stat>>;
SEQ ::= "seq" | "par";
// ---------------------------------------------------------------------------------------------------------------------
while_statement ::= WHILE namespace [WITH identifier] <<brace inner_stat>>;
WHILE ::= "while";
WITH ::= "with";
// ---------------------------------------------------------------------------------------------------------------------
normal_statement ::= identifier <<brace inner_stat>>;
// ---------------------------------------------------------------------------------------------------------------------
annotation ::= annotation_mark;
annotation_mark ::= AT SYMBOL?;
// ---------------------------------------------------------------------------------------------------------------------
expression ::= lhs EQ rhs;
rhs ::= value [QUESTION value];
lhs ::= namespace slice?;
// ---------------------------------------------------------------------------------------------------------------------
value ::= call | namespace | NUMBER BITS?
// ---------------------------------------------------------------------------------------------------------------------
call ::= identifier <<parenthesis rhs COMMA>>;
// ---------------------------------------------------------------------------------------------------------------------
slice ::= <<bracket identifier COMMA>>
// ---------------------------------------------------------------------------------------------------------------------
namespace ::= identifier (DOT identifier)*;
identifier ::= SYMBOL
// ---------------------------------------------------------------------------------------------------------------------
meta parenthesis ::= PARENTHESIS_L [<<param>> (<<sep>> <<param>>)* [<<sep>>]] PARENTHESIS_R;
meta bracket ::= BRACKET_L [<<param>> (<<sep>> <<param>>)* [<<sep>>]] BRACKET_R;
meta brace   ::= BRACE_L <<param>>* BRACE_R
meta angle   ::= ANGLE_L [<<param>> (<<sep>> <<param>>)* [<<sep>>]] ANGLE_R